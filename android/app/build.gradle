plugins {
    id 'com.android.application'
}

android {
    namespace 'com.blitterstudio.amiberry'
    compileSdk 36

    defaultConfig {
        applicationId "com.blitterstudio.amiberry"
        minSdk 24
        targetSdk 34
        versionCode 1
        versionName "1.0"

        externalNativeBuild {
            cmake {
                cppFlags "-std=c++17"
                arguments "-DANDROID_STL=c++_shared", "-DUSE_OPENGL=ON"
            }
        }
        
        ndk {
            abiFilters 'arm64-v8a', 'x86_64'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    
    externalNativeBuild {
        cmake {
            path file('../../CMakeLists.txt')
            version '3.22.1'
        }
    }
    
    buildFeatures {
        prefab true
        viewBinding true
    }
    
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'com.google.android.material:material:1.13.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    // SDL2 is now built from source via CMake

}


tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation"
}

task copyAssets(type: Copy) {
    description = 'Copies Amiberry data files to Android assets'
    into 'src/main/assets'
    
    from('../../data') {
        into 'data'
    }
    from('../../controllers') {
        into 'controllers'
    }
    from('../../roms') {
        into 'roms'
    }
    from('../../whdboot') {
        into 'whdboot'
    }
}

preBuild.dependsOn copyAssets
